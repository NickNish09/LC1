% ----------------------------------------------------------%
% tca_3D_interval - Time of closest approach for cylindrical distance
% between the two aircraft during a time interval [B,T]
% ----------------------------------------------------------% 

tca_3D_interval[D,H:posreal,B:nnreal,T: {AB: posreal|AB>B}]  : THEORY 
BEGIN

  IMPORTING tca_3D[D,H],
  	    cd3d[D,H,B,T]

  s,v 	    : VAR Vect3       
  t,t1,t2,r : VAR Lookahead[B,T]
  B1,B2	    : VAR real
  eps	    : VAR Sign
  nnt	    : VAR nnreal

  tca_3D_interval(s,v): Lookahead[B,T] = 
  			LET tca = tca_3D(s,v)
			IN
			    IF B<=tca AND tca<=T THEN tca
			    ELSIF tca<B THEN B
			    ELSE T
			    ENDIF

  tca_3D_interval_def: LEMMA cyl_norm_sq(s + t*v) >= cyl_norm_sq(s+tca_3D_interval(s,v)*v)

  tca_3D_interval_conflict: LEMMA conflict_3D?(s,v) IFF cyl_norm_sq(s + tca_3D_interval(s,v)*v) < 1

  tca_3D_interval_conflict_at: LEMMA conflict_3D?(s,v) IFF conflict_at?(s,v,tca_3D_interval(s,v))







END tca_3D_interval
