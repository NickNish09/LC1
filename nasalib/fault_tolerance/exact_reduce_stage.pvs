
%
%
% Purpose : the validity and agreement properties of a reduce_select
%           choice function in the exact communication case
%
%


exact_reduce_stage[
  S:    posnat,
  D:    posnat,
  T:    TYPE+,
  <=: (total_order?[T]),
  error: T
]: THEORY  


  BEGIN

  IMPORTING 
    reduce_choice[S, D, T, <=, error],
    exact_comm_stage[S, D, T]

  s: VAR below(S)
  d, d1, d2: VAR below(D)
  v: VAR T
 
  src_set: VAR non_empty_finite_set[below(S)]
  dst_check: VAR non_empty_finite_set[below(D)]

  sent: VAR sent_vec_stage
  rcvd: VAR rcvd_matrix_stage
  check: VAR check_stage

  cf: VAR consensus_function

  tau: VAR tau_type

  n, n1, n2: VAR nat

  reduce_lower_validity: LEMMA
      quorum_correct?(sent, rcvd, check, tau) 
    IMPLIES
      EXISTS s: enabled(rcvd, check)(d)(s) AND
        sent(s) <= reduce_choice(tau, cf)(rcvd, check)(d)

  reduce_upper_validity: LEMMA
      quorum_correct?(sent, rcvd, check, tau) 
    IMPLIES
      EXISTS s: enabled(rcvd, check)(d)(s) AND
        reduce_choice(tau, cf)(rcvd, check)(d) <= sent(s)

  reduce_agreement_generation: LEMMA
      all_symmetric?(rcvd, check)
    IMPLIES
      reduce_choice(tau, cf)(rcvd, check)(d1) = reduce_choice(tau, cf)(rcvd, check)(d2)


  IMPORTING select_minmax[S, T, <=], select_minmax[D, T, <=]

  reduce_min_validity: LEMMA
      quorum_correct?(sent, rcvd, check, tau) AND
      enabled_within?(rcvd, check)(src_set)
    IMPLIES
      v_min(sent, src_set) 
       <= v_min(reduce_choice(tau, cf)(rcvd, check), dst_check)

  reduce_max_validity: LEMMA
      quorum_correct?(sent, rcvd, check, tau) AND
      enabled_within?(rcvd, check)(src_set)
    IMPLIES
      v_max(reduce_choice(tau, cf)(rcvd, check), dst_check)
        <= v_max(sent, src_set)

  reduce_consensus: LEMMA
      quorum_correct?(sent, rcvd, check, tau) AND
      enabled_within?(rcvd, check)(src_set) AND
      uniform?(sent, v)(src_set)
    IMPLIES
      uniform?(reduce_choice(tau, cf)(rcvd, check), v)(dst_check)

  reduce_min_eq_max: LEMMA
      all_symmetric?(rcvd, check)
    IMPLIES
      v_max(reduce_choice(tau, cf)(rcvd, check), dst_check) =
      v_min(reduce_choice(tau, cf)(rcvd, check), dst_check)

  END exact_reduce_stage
