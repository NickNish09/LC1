real_metric_space: THEORY
%------------------------------------------------------------------------------
% The metric space structure on the reals
%
%     Authors: Anthony Narkawicz,  NASA Langley
%
%     Version 1.0         8/28/2009  Initial Version
%------------------------------------------------------------------------------

BEGIN

  x,y: VAR real

  real_dist(x,y): nnreal = abs(x-y)

  IMPORTING metric_spaces_def

  S: VAR set[real]

  real_metric_space: LEMMA metric_space?[real,real_dist](S)


  IMPORTING open_sets[real], compactness[real,real_dist], convergence_sequences


  compact_induction: LEMMA FORALL (c: real, d: {x: real | c < x}): 
     (compact?[real,real_dist](closed_intv(c,(c+d)/2))
     AND compact?[real,real_dist](closed_intv((c+d)/2,d))) 
        IMPLIES compact?[real,real_dist](closed_intv(c,d))


  seq_intv_scaf(c: real, d: {x: real | c < x}, 
                YS: {XST: set[set[real]] | open_cover?(XST,closed_intv(c,d))})(n: nat): 
  RECURSIVE [real,real] = 
        IF n = 0 THEN (c,d) 
        ELSIF
           NOT EXISTS (XS: set[set[real]]): subset?(XS,YS) 
           AND finite_cover?(XS,closed_intv(seq_intv_scaf(c,d,YS)(n-1)`1,
                             (seq_intv_scaf(c,d,YS)(n-1)`1+seq_intv_scaf(c,d,YS)(n-1)`2)/2))
        THEN
           (seq_intv_scaf(c,d,YS)(n-1)`1,(seq_intv_scaf(c,d,YS)(n-1)`1+seq_intv_scaf(c,d,YS)(n-1)`2)/2)
        ELSE
           ((seq_intv_scaf(c,d,YS)(n-1)`1+seq_intv_scaf(c,d,YS)(n-1)`2)/2,seq_intv_scaf(c,d,YS)(n-1)`2)
        ENDIF MEASURE n


  seq_inv_scaf_decreasing: LEMMA 
     FORALL (c: real, d: {x: real | c < x},YS: {XST: set[set[real]] | 
             open_cover?(XST,closed_intv(c,d))}): 
     FORALL (n:nat): seq_intv_scaf(c,d,YS)(n)`1 < seq_intv_scaf(c,d,YS)(n)`2
        AND subset?(closed_intv(seq_intv_scaf(c,d,YS)(n+1)`1,seq_intv_scaf(c,d,YS)(n+1)`2),
                    closed_intv(seq_intv_scaf(c,d,YS)(n)`1,seq_intv_scaf(c,d,YS)(n)`2))
        AND seq_intv_scaf(c,d,YS)(n)`2 - seq_intv_scaf(c,d,YS)(n)`1 = (d-c)/(2^n)


  compact_seq_induction: LEMMA FORALL (c: real, d: {x: real | c < x}):
      (NOT compact?[real,real_dist](closed_intv(c,d))) 
      IMPLIES
           EXISTS (YS: set[set[real]]): open_cover?(YS,closed_intv(c,d)) AND
               FORALL (n:nat): LET W = seq_intv_scaf(c,d,YS)(n) IN  
                     (NOT EXISTS (XS: set[set[real]]): subset?(XS,YS) AND
                                  finite_cover?(XS,closed_intv(W`1,W`2)))

  % ---------------- Closed intervals over the reals are compact ----------------------

  closed_intervals_compact: THEOREM FORALL (a: real, b: {x: real | a < x}): 
     compact?[real,real_dist](closed_intv(a,b))

END real_metric_space
