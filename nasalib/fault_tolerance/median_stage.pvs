median_stage[
  S:    posnat,
  D:    posnat,
  error: real
]: THEORY  


  BEGIN

  IMPORTING 
    inexact_reduce_stage[S, D, error],
    median_overlap[S, real, <=]

  d, d1, d2: VAR below(D)
  epsilon, delta: VAR nonneg_real

  sent: VAR sent_vec_stage
  rcvd: VAR rcvd_matrix_stage
  check: VAR check_stage

  src_set: VAR non_empty_finite_set[below(S)]
  dst_set: VAR non_empty_finite_set[below(D)]

  median: choice_stage = reduce_choice(mid, midpoint)
    
  one_asymmetric_ommission?(rcvd, check): bool = 
    FORALL d1, d2:
      card[below(S)](union(enabled(rcvd, check)(d1), enabled(rcvd, check)(d2))) -
        card[below(S)](intersection(enabled(rcvd, check)(d1), enabled(rcvd, check)(d2))) <= 1

  one_asymmetric_overlap: LEMMA
      enabled_nonempty?(rcvd, check) AND
      one_asymmetric_ommission?(rcvd, check) AND
      all_single_imprecision?(rcvd, check, epsilon)
    IMPLIES
      overlap_imprecision?(rcvd, check, mid, epsilon)

  median_convergence: THEOREM 
      quorum_imprecision?(sent, rcvd, check, mid, epsilon) AND
      one_asymmetric_ommission?(rcvd, check) AND
      all_single_imprecision?(rcvd, check, epsilon) AND
      enabled_within?(rcvd, check)(src_set) AND
      v_max(sent, src_set) - v_min(sent, src_set) <= delta
    IMPLIES
      v_max(median(rcvd, check), dst_set) - v_min(median(rcvd, check), dst_set) <= (delta / 2) + epsilon

  END median_stage
