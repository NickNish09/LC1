%-----------------------------------------------------------------------------
% The conjunction of filters with mapping functions on sequences of countable
% length.
%
% Author: Jerry James <loganjerry@gmail.com>
%
% This file and its accompanying proof file are distributed under the CC0 1.0
% Universal license: http://creativecommons.org/publicdomain/zero/1.0/.
%
% Version history:
%   2007 Feb 14: PVS 4.0 version
%   2011 May  6: PVS 5.0 version
%   2013 Jan 14: PVS 6.0 version
%-----------------------------------------------------------------------------
csequence_filter_map[T1, T2: TYPE]: THEORY
 BEGIN

  IMPORTING csequence_map_props[T1, T2]
  IMPORTING csequence_filter[T1], csequence_filter[T2]

  n: VAR nat
  p: VAR pred[T2]
  f: VAR [T1 -> T2]
  cseq: VAR csequence[T1]

  map_first_p: THEOREM
    FORALL f, p, cseq:
      some(p)(map(f, cseq)) IMPLIES
       first_p(p, map(f, cseq)) = first_p(p o f, cseq)

  map_suffix_empty: THEOREM
    FORALL f, cseq, n:
      empty?(suffix(map(f, cseq), n)) IFF empty?(suffix(cseq, n))

  map_suffix: THEOREM
    FORALL f, cseq, n: suffix(map(f, cseq), n) = map(f, suffix(cseq, n))

  filter_map: THEOREM
    FORALL f, p, cseq: filter(map(f, cseq), p) = map(f, filter(cseq, p o f))

 END csequence_filter_map
