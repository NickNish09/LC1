%------------------------------------------------------------------------------
% Composition
%
% All references are to WA Sutherland "Introduction to Metric and
% Topological Spaces", OUP, 1981
%
%     Author: David Lester, Manchester University, NIA, UniversitÃ© Perpignan
%
%     Version 1.0            5/12/04  Initial Version
%     Version 1.1            8/6/06   Judgements added, Interface changed
%------------------------------------------------------------------------------

composition_continuity[T1:Type,(IMPORTING topology_def[T1]) S:topology[T1],
                       T2:Type,(IMPORTING topology_def[T2]) T:topology[T2],
                       T3:Type,(IMPORTING topology_def[T3]) U:topology[T3]]:
                                                                         THEORY

BEGIN

  IMPORTING continuity[T1,S,T2,T],
            continuity[T2,T,T3,U],
            continuity[T1,S,T3,U]

  f: VAR [T2->T3]
  g: VAR [T1->T2]
  x: VAR T1

  composition_continuous_at: LEMMA
    continuous_at?(g,x) AND continuous_at?(f,g(x)) => continuous_at?(f o g, x)

  composition_continuous: LEMMA continuous?(g) AND continuous?(f) =>
                                continuous?(f o g)

  F: VAR continuous[T2,T,T3,U]
  G: VAR continuous[T1,S,T2,T]

  composition_is_continuous: JUDGEMENT o(F,G) HAS_TYPE continuous[T1,S,T3,U]

END composition_continuity
