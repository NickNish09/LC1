%------------------------------------------------------------------------------
% opt_line.pvs
% ACCoRD v.1.0
%
% Optimal lines solutions are ground speed/track optimal solutions that are 
% tangent to the protected zone.
%------------------------------------------------------------------------------

opt_line[D: posreal] : THEORY
BEGIN
  
  IMPORTING opt_trk_gs[D],
            tangent_line[D]

  sp      : VAR Sp_vect2
  eps     : VAR Sign
  vo,vi,
  nvo     : VAR Vect2

  %------------%
  % ALGORITHMS %
  %------------%

  % zero indicates no optimal horizontal solution

  opt_line_eps(sp,vo,vi,eps): {nvo:Vect2 | nz_vect2?(nvo) => 
                                           optimal?(vo,tangent_line(sp,eps))(nvo)} =
    LET (k,nvo) = opt_trk_gs_line(tangent_line(sp,eps),vo,vi) IN
    IF k >= 0 THEN
      nvo
    ELSE
      zero 
    ENDIF


  opt_line_eps?(sp,vo,vi,eps)(nvo) : bool =
    nz_vect2?(nvo) AND
    nvo = opt_line_eps(sp,vo,vi,eps)

  %----------%
  % LEMMAS   %
  %----------%

  opt_line_eps_tangent_line: LEMMA 
    opt_line_eps?(sp,vo,vi,eps)(nvo) 
    IMPLIES
      tangent_line?(sp,nvo-vi,eps)  

  opt_line_is_line_solution: LEMMA 
    opt_line_eps?(sp,vo,vi,eps)(nvo) 
    IMPLIES
      line_solution?(sp,nvo-vi,eps)              

END opt_line
