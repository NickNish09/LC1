%------------------------------------------------------------------------------
% Properties of sigma-finite measures
%
%     Author: David Lester, Manchester University, NIA, UniversitÃ© Perpignan
%
% All references are to SK Berberian "Fundamentals of Real Analysis",
% Springer, 1991
%
%     Version 1.0            1/5/07   Initial Version
%------------------------------------------------------------------------------

sigma_finite_measure_props[T:TYPE,          (IMPORTING subset_algebra_def[T])
                           S:sigma_algebra, (IMPORTING measure_def[T,S])
                           mu:sigma_finite_measure]: THEORY

BEGIN

  IMPORTING measure_contraction_props[T,S,mu],
            measure_equality[T,S]

  f: VAR nn_integrable[T,S,mu]
  g: VAR integrable[T,S,mu]
  h: VAR nn_measurable[T,S]
  A: VAR (S)
  x: VAR T
  n: VAR nat
  F: VAR sequence[[T->real]]

  convergent?: MACRO pred[sequence[real]] = topological_convergence.convergent?

  sfm_integrable: LEMMA
    ((FORALL n: integrable?[T,S,contraction(mu,A_of(mu)(n))](h)) AND
     convergent?(series(lambda n:
                          integral[T,S,contraction(mu,A_of(mu)(n))](h))))
    <=> integrable?[T,S,mu](h)

  sfm_integral: LEMMA
   convergence?(series(lambda n: integral[T,S,contraction(mu,A_of(mu)(n))](f)),
                integral[T,S,mu](f))

  sfm_component_eq: LEMMA x_eq(to_measure(fm_contraction(mu,A_of(mu)(n)))(A),
                               contraction(mu,A_of(mu)(n))(A))

  IMPORTING integral_convergence[T,S,mu]

  sfm_monotone_convergence: LEMMA                                       % 7.4.5
    ae_increasing?(F) AND
    (FORALL n,x: F(n)(x) >= 0) AND
    (FORALL n: integrable?[T,S,contraction(mu,P_of(mu)(n))](F(n))) =>
    (((EXISTS g:
         ae_convergence?(F,g)) <=>
         bounded?(lambda n: integral[T,S,contraction(mu,P_of(mu)(n))](F(n))))
                                                                            AND
       FORALL g:
         ae_convergence?(F,g) =>
         converges_upto?(lambda n:
                 integral[T,S,contraction(mu,P_of(mu)(n))](F(n)), integral(g)))

END sigma_finite_measure_props
